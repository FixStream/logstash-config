// Parsing rules based on https://github.com/elastic/logstash/blob/master/logstash-core/lib/logstash/config/grammar.treetop
{
    package config
}

// Parsing entry point
init =
    &{
        return initParser();
    } conf:config EOF {
        return ret(conf)
    } // &{ return pushError("empty config file?", c) }

//  rule config
//    _ plugin_section _ (_ plugin_section)* _ <LogStash::Config::AST::Config>
//  end

config =
    _ ps:plugin_section _ pss:(
        _ ps:plugin_section { 
            return ret(ps)
        } 
    )* _ EOF { 
        return config(ps, pss) 
    }

//  rule comment
//    (whitespace? "#" [^\r\n]* "\r"? "\n")+ <LogStash::Config::AST::Comment>
//  end

comment =
    (whitespace? "#" [^\r\n]* "\r"? "\n")+

//  rule _
//    (comment / whitespace)* <LogStash::Config::AST::Whitespace>
//  end

_ =
    (comment / whitespace)*

//  rule whitespace
//    [ \t\r\n]+ <LogStash::Config::AST::Whitespace>
//  end

whitespace =
    [ \t\r\n]+

//  rule plugin_section
//    plugin_type _ "{"
//       _ (branch_or_plugin _)*
//    "}"
//    <LogStash::Config::AST::PluginSection>
//  end

plugin_section =
    pt:plugin_type _ "{" _ bops:(
        bop:branch_or_plugin _ { 
            return ret(bop) 
        } 
    )* (
        "}" / &{
            return pushError("expect closing curly bracket", c)
        }
    ) {
        return pluginSection(pt, bops) 
    }

//   rule branch_or_plugin
//     branch / plugin
//   end

branch_or_plugin =
    branch / plugin

//   rule plugin_type
//     ("input" / "filter" / "output")
//   end

plugin_type =
    "input" {
        return ast.Input, nil
    } / "filter" {
        return ast.Filter, nil
    } / "output" {
        return ast.Output, nil
    } / &{
        return pushError("expect plugin type (input, filter, output)", c)
    }

//   rule plugins
//     (plugin (_ plugin)*)?
//     <LogStash::Config::AST::Plugins>
//   end

// plugins =
//     (
//         p:plugin ps:(
//             _ p:plugin {
//                 fmt.Println("plugin")
//                 return ret(p)
//             }
//         )* {
//             fmt.Println("plugins")
//             return plugins(p, ps)
//         }
//     )?

//   rule plugin
//     name _ "{"
//       _
//       attributes:( attribute (whitespace _ attribute)*)?
//       _
//     "}"
//     <LogStash::Config::AST::Plugin>
//   end

plugin =
    name:name _ "{" _ attributes:( 
        attribute:attribute attrs:(
            whitespace _ attribute:attribute {
                return ret(attribute)
            }
        )* {
            return attributes(attribute, attrs)
        }
    )? _ (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return plugin(name, attributes)
    }

//   rule name
//     (
//       ([A-Za-z0-9_-]+ <LogStash::Config::AST::Name>)
//       / string
//     )
//   end

name =
    ( ([A-Za-z0-9_-]+) {
        return string(c.text), nil
    } / value:string_value {
        return ret(value)
    } )

//   rule attribute
//     name _ "=>" _ value
//     <LogStash::Config::AST::Attribute>
//   end

attribute =
    name:name _ "=>" _ value:value {
        return attribute(name, value)
    }

//   rule value
//     plugin / bareword / string / number / array / hash
//   end

value =
    plugin / bareword / string_value / number / array / hash / &{
        return fatalError("invalid value", c)
    }

//   rule array_value
//     bareword / string / number / array / hash
//   end

array_value =
    bareword / string_value / number / array / hash / &{
        return fatalError("invalid array value", c)
    }

//   rule bareword
//     [A-Za-z_] [A-Za-z0-9_]+
//     <LogStash::Config::AST::Bareword>
//   end

bareword =
    [A-Za-z_] [A-Za-z0-9_]+ {
        return ast.NewStringAttribute("", string(c.text), ast.Bareword), nil
    }

//   rule double_quoted_string
//     ( '"' ( '\"' / !'"' . )* '"' <LogStash::Config::AST::String>)
//   end

double_quoted_string =
    ( `"` ( `\"` / !`"` . )* (
        `"` / &{
            return fatalError("expect closing double quotes (\")", c)
        }
    ) ) {
        return enclosedValue(c)
    }

//   rule single_quoted_string
//     ( "'" ( "\\'" / !"'" . )* "'" <LogStash::Config::AST::String>)
//   end

single_quoted_string =
    ( `'` ( `\'` / !`'` . )* (
        `'` / &{
            return fatalError("expect closing single quote (')", c)
        }
    ) ) {
        return enclosedValue(c)
    }

//   rule string
//     double_quoted_string / single_quoted_string
//   end

string_value =
    str:( str:double_quoted_string {
        return ast.NewStringAttribute("", str.(string), ast.DoubleQuoted), nil
    } / str:single_quoted_string {
        return ast.NewStringAttribute("", str.(string), ast.SingleQuoted), nil
    } ) {
        return ret(str)
    }

//   rule regexp
//     ( '/' ( '\/' / !'/' . )* '/'  <LogStash::Config::AST::RegExp>)
//   end

regexp =
    ( `/` ( `\/` / !`/` . )* (
        `/` / &{
            return fatalError("expect closing slash (/) for regexp", c)
        }
    ) ) {
        return regexp(c)
    }

//   rule number
//     "-"? [0-9]+ ("." [0-9]*)?
//     <LogStash::Config::AST::Number>
//   end

number =
    "-"? [0-9]+ ("." [0-9]*)? {
        return number(string(c.text))
    }

//   rule array
//     "["
//     _
//     (
//       value (_ "," _ value)*
//     )?
//     _
//     "]"
//     <LogStash::Config::AST::Array>
//   end

// TODO: value should be changed to to array_value due to https://github.com/elastic/logstash/issues/6580
array =
    "[" _ value:( 
        value:value values:(
            _ "," _ value:value {
                return ret(value)
            }
        )* {
            return attributes(value, values)
        }
    )? _ (
        "]" / &{
            return fatalError("expect closing square bracket", c)
        }
    ) {
        return array(value)
    }

//   rule hash
//     "{"
//       _
//       hashentries?
//       _
//     "}"
//     <LogStash::Config::AST::Hash>
//   end

hash =
    "{" _ entries:hashentries? _ (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return hash(entries)
    }

//   rule hashentries
//     hashentry (whitespace hashentry)*
//     <LogStash::Config::AST::HashEntries>
//   end

hashentries =
    hashentry:hashentry hashentries1:(
        whitespace hashentry:hashentry {
            return ret(hashentry)
        }
    )* {
        return hashentries(hashentry, hashentries1)
    }

//   rule hashentry
//     name:(number / bareword / string) _ "=>" _ value
//     <LogStash::Config::AST::HashEntry>
//   end

hashentry =
    name:(number / bareword / string_value) _ "=>" _ value:value {
        return hashentry(name, value)
    }

// Conditions

//   rule branch
//     if (_ else_if)* (_ else)?
//     <LogStash::Config::AST::Branch>
//   end

branch =
    ifBlock:if_cond elseIfBlocks:(
        _ eib:else_if {
                return ret(eib)
            }
        )* elseBlock:(
            _ eb:else_cond {
                return ret(eb)
            }
        )? {
        return branch(ifBlock, elseIfBlocks, elseBlock)
    }

//   rule if
//     "if" _ condition _ "{" _ (branch_or_plugin _)* "}"
//     <LogStash::Config::AST::If>
//   end

if_cond =
    "if" _ cond:condition _ "{" _ bops:(
            bop:branch_or_plugin _ {
                return ret(bop)
            }
        )* (
            "}" / &{
                return fatalError("expect closing curly bracket", c)
            }
        ) {
        return ifBlock(cond, bops)
    }

//   rule else_if
//     "else" _ "if" _ condition _ "{" _ ( branch_or_plugin _)* "}"
//     <LogStash::Config::AST::Elsif>
//   end

else_if =
    "else" _ "if" _ cond:condition _ "{" _ bops:(
        bop:branch_or_plugin _ {
            return ret(bop)
        }
    )* (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return elseIfBlock(cond, bops)
    }

//   rule else
//     "else" _ "{" _ (branch_or_plugin _)* "}"
//     <LogStash::Config::AST::Else>
//   end

else_cond =
    "else" _ "{" _ bops:(
        bop:branch_or_plugin _ {
            return ret(bop)
        }
    )* (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return elseBlock(bops)
    }

//   rule condition
//     expression (_ boolean_operator _ expression)*
//     <LogStash::Config::AST::Condition>
//   end

condition =
    cond:expression conds:(
        _ bo:boolean_operator _ cond:expression {
            return expression(bo, cond)
        }
    )* {
        return condition(cond, conds)
    }

//   rule expression
//     (
//         ("(" _ condition _ ")")
//       / negative_expression
//       / in_expression
//       / not_in_expression
//       / compare_expression
//       / regexp_expression
//       / rvalue
//     ) <LogStash::Config::AST::Expression>
//   end

expression = 
    (
        (
            "(" _ cond:condition _ ")" {
                return condition_expression(cond)
            }
        )
      / negative_expression
      / in_expression
      / not_in_expression
      / compare_expression
      / regexp_expression
      / rv:rvalue {
          return rvalue(rv)
      }
      // &{ return pushError("expect valid expression", c) }
    )

//   rule negative_expression
//     (
//         ("!" _ "(" _ condition _ ")")
//       / ("!" _ selector)
//     ) <LogStash::Config::AST::NegativeExpression>
//   end

negative_expression = 
    (
        (
            "!" _ "(" _ cond:condition _ (
                ")" / &{
                    return fatalError("expect closing parenthesis", c)
                }
            ) {
                return negative_expression(cond)
            }
        )
      / (
          "!" _ sel:selector {
              return negative_selector(sel)
          }
        )
    )

//   rule in_expression
//     rvalue _ in_operator _ rvalue
//     <LogStash::Config::AST::InExpression>
//   end

in_expression =
    lv:rvalue _ in_operator _ rv:rvalue {
        return in_expression(lv, rv)
    }

//   rule not_in_expression
//     rvalue _ not_in_operator _ rvalue
//     <LogStash::Config::AST::NotInExpression>
//   end

not_in_expression =
    lv:rvalue _ not_in_operator _ rv:rvalue {
        return not_in_expression(lv, rv)
    }

//   rule in_operator
//     "in"
//   end

in_operator =
    "in" / &{
        return pushError("expect in operator (in)", c)
    }

//   rule not_in_operator
//     "not " _ "in"
//   end

not_in_operator =
    "not " _ "in" / &{
        return pushError("expect not in operator (not in)", c)
    }

//   rule rvalue
//     string / number / selector / array / method_call / regexp
//   end

// Removed method_call due to https://github.com/elastic/logstash/issues/6698
// rvalue =
//     string_value / number / selector / array / method_call / regexp
rvalue =
    string_value / number / selector / array / regexp / &{
        return pushError("invalid value for expression", c)
    }

//   rule method_call
//       method _ "(" _
//         (
//           rvalue ( _ "," _ rvalue )*
//         )?
//       _ ")"
//     <LogStash::Config::AST::MethodCall>
//   end

// Removed method_call due to https://github.com/elastic/logstash/issues/6698
// method_call  =
//       method _ "(" _
//         (
//           rvalue ( _ "," _ rvalue )*
//         )?
//       _ ")"

//   rule method
//     bareword
//   end

// Removed method_call due to https://github.com/elastic/logstash/issues/6698
// method =
//     bareword

//   rule compare_expression
//     rvalue _ compare_operator _ rvalue
//     <LogStash::Config::AST::ComparisonExpression>
//   end

compare_expression =
    lv:rvalue _ co:compare_operator _ rv:rvalue {
        return compare_expression(lv, co, rv)
    }

//   rule compare_operator 
//     ("==" / "!=" / "<=" / ">=" / "<" / ">") 
//     <LogStash::Config::AST::ComparisonOperator>
//   end

compare_operator =
    ("==" / "!=" / "<=" / ">=" / "<" / ">" / &{
        return pushError("expect compare operator (==, !=, <=, >=, <, >)", c)
    }) {
        return compare_operator(string(c.text))
    }

//   rule regexp_expression
//     rvalue _  regexp_operator _ (string / regexp)
//     <LogStash::Config::AST::RegexpExpression>
//   end

regexp_expression =
    lv:rvalue _  ro:regexp_operator _ rv:(string_value / regexp) {
        return regexp_expression(lv, ro, rv)
    }

//   rule regexp_operator
//     ("=~" / "!~") <LogStash::Config::AST::RegExpOperator>
//   end

regexp_operator =
    ("=~" / "!~" / &{
        return pushError("expect regexp comparison operator (=~, !~)", c)
    }) {
        return regexp_operator(string(c.text))
    }

//   rule boolean_operator
//     ("and" / "or" / "xor" / "nand")
//     <LogStash::Config::AST::BooleanOperator>
//   end

boolean_operator =
    ("and" / "or" / "xor" / "nand" / &{
        return pushError("expect boolean operator (and, or, xor, nand)", c)
    }) {
        return boolean_operator(string(c.text))
    }

//   rule selector
//     selector_element+
//     <LogStash::Config::AST::Selector>
//   end

selector =
    ses:selector_element+ {
        return selector(ses)
    }

//   rule selector_element
//     "[" [^\],]+ "]"
//     <LogStash::Config::AST::SelectorElement>
//   end

selector_element =
    "[" [^\],]+ (
        "]" / &{
            return pushError("expect closing square bracket", c)
        }
    ) {
        return selector_element(string(c.text))
    }

EOF = !.